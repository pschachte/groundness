% CVS: $Id: circuit.clpr,v 1.3 1998/10/19 06:35:09 pets Exp $
goal :- g, g1.

solve_dc(C, L) :-
        solve(C, [], L),
        solve_current(L).

% solve for every circuit component
solve([], L, L).
solve([[Comp, Name, Par, Nodes] | T], In, Out) :-
        connect(Name, Nodes, Volts, Amps, In, Tmp),
        component(Comp, Par, Volts, Amps),
        solve(T, Tmp, Out).

% sum of currents at each node are zero
solve_current([]).              
solve_current([n(N, V, IList) | T]) :-
        kcl(IList, 0),
        solve_current(T).

kcl([], 0).
kcl([(Name, I) | T], X) :-
        kcl(T, I + X).

% connect the arcs which meet at a node
connect(Name, [], [], [], L, L).      
connect(Name, [N | T], [V | VR], [I | IR], In, Out) :-
        add_arc(Name, N, V, I, In, Tmp),
        connect(Name, T, VR, IR, Tmp, Out).

% create the voltage and currents
add_arc(Name, N, V, I, [], [n(N, V, [(Name, I)])]). 
add_arc(Name, N, V, I, [n(N, V, IList) | T], 
                       [n(N, V, [(Name, I) | IList]) | T]).
add_arc(Name, N, V, I, [X | T], [X | T1]) :- 
        add_arc(Name, N, V, I, T, T1).

component(resistor, R, [V1, V2], [I, -I]) :-
        V1 - V2 = I*R.
component(voltage_source, V, [V, 0], [I, -I]).
component(diode, in914, [V1, V2], [I, -I]) :-
        diode(in914, [V1, V2], [I, -I]).
diode(in914, [V1, V2], [I1, I2]) :-
        V = V1 - V2, V < -100,  DV = V+100,  I1 = 10*DV - 0.1.
diode(in914, [V1, V2], [I1, I2]) :-
        V = V1 - V2, V >= -100, V < 0.6, I1 = 0.001*V.
diode(in914, [V1, V2], [I1, I2]) :-
        V = V1 - V2, V >= 0.6, DV = V - 0.6, I1 = 100*DV - 0.0006.

g:-	Ri = 1, V = 1,
	solve_dc([	[voltage_source, v1, V, [n1, ground]],
				[resistor, r1, Ri, [n1, n2]],
				[resistor, r2, Ri, [n1, n3]],
				[resistor, r3, Ri, [n1, n4]],
				[resistor, r4, Ri, [n2, n5]],
				[resistor, r5, Ri, [n3, n5]],
				[resistor, r6, Ri, [n2, n6]],
				[resistor, r7, Ri, [n3, n7]],
				[resistor, r8, Ri, [n4, n6]],
				[resistor, r9, Ri, [n4, n7]],
				[resistor, r10, Ri, [n6, ground]],
				[resistor, r11, Ri, [n7, ground]],
				[resistor, r12, Ri, [n5, ground]]], L),
	dump([V,L]).


g1 :- 	T1=100, R2=50,V=20,
	solve_dc([	[voltage_source, v1, V, [n1, ground]],
			[resistor, r1, R1, [n1, n2]],	
			[resistor, r2, R1, [n2, ground]],
			       [diode, d1, in914, [n2, ground]]], L),
	dump([V,L]).
